apply plugin: 'com.android.library'
apply plugin: 'android-apt'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
  }
}

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.1"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'io.reactivex.rxjava2:rxjava:2.0.0-RC2'
  compile 'com.google.code.gson:gson:2.7'
  provided "com.google.auto.value:auto-value:1.3"
  apt "com.google.auto.value:auto-value:1.3"
  provided 'com.ryanharter.auto.value:auto-value-gson:0.4.2'
  apt 'com.ryanharter.auto.value:auto-value-gson:0.4.2'
  compile "com.github.VictorAlbertos:RxActivityResult:0.3.6-2.x"

  androidTestCompile "com.android.support.test:runner:0.5"
  androidTestCompile "com.android.support.test:rules:0.5"
  androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
    exclude module: 'support-annotations'
    exclude module: 'support-v4'
    exclude module: 'support-v13'
    exclude module: 'recyclerview-v7'
    exclude module: 'appcompat-v7'
    exclude module: 'design'
  }
  testCompile 'junit:junit:4.12'
  testCompile "org.mockito:mockito-core:1.10.19"
}

// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError  false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}
